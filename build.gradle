import org.flywaydb.gradle.task.FlywayInfoTask
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript { // for doma-codegen
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.mysql:mysql-connector-j:8.0.33'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'groovy'
    id 'eclipse'
    id 'com.diffplug.eclipse.apt' version '3.42.2' // Eclipseビルド時のapt
    id 'idea'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.seasar.doma.compile' version '1.1.0'
    id "org.flywaydb.flyway" version "8.2.0" // これより上記バージョンでは動作しない
    id 'org.domaframework.doma.codegen' version '2.0.0'
    id 'jacoco'
    id 'org.sonarqube' version '4.3.0.3225'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.7.0'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation('commons-beanutils:commons-beanutils:1.9.4') {
        exclude module: 'commons-logging'
    }
    implementation 'org.seasar.doma:doma-core:2.54.0'
    implementation 'com.zaxxer:HikariCP'
    implementation 'com.google.guava:guava:32.1.2-jre'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.seasar.doma:doma-processor:2.54.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation('com.github.springtestdbunit:spring-test-dbunit:1.3.0') {
        exclude module: 'junit'
        exclude module: 'dbunit'
        exclude module: 'postgresql'
    }
    testImplementation('org.dbunit:dbunit:2.7.3') {
        exclude module: 'postgresql'
        exclude module: 'ojdbc8'
    }
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0' // SpringBoot3系では2.4以降
}

sourceSets {
    generated {
        java {
            srcDirs 'generated/src/main/java'
        }
    }
}

def domaResources = ['doma.compile.config', 'META-INF/**/*.sql', 'META-INF/**/*.script']

tasks.register('copyDomaResourcesJava', Copy) {
    from sourceSets.main.resources.srcDirs
    into compileJava.destinationDirectory
    include domaResources
}

compileJava {
    options.encoding = 'UTF-8'
    dependsOn copyDomaResourcesJava
}

processResources {
    exclude domaResources
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs = ['-Dspring.profiles.active=ut']
}

tasks.named('eclipse') {
    mkdir "$projectDir/.apt_generated"
}
eclipse {
    project {
        // Eclipseではgroovyソースはソースリンクにすることでコンパイルされるので作成
        linkedResource name: 'test_groovy', type: '2', location: "$projectDir/src/test/groovy".toString()
    }
    classpath {
        file {
            whenMerged { classpath ->
                classpath.entries.removeAll { it.path == '.apt_generated' }
                classpath.entries.removeAll { it.path == 'test_groovy' }
                classpath.entries.removeAll { it.path == 'src/test/groovy' }
            }
            withXml { provider ->
                def node = provider.asNode()
                // specify output path for .apt_generated / test_groovy
                node.appendNode('classpathentry', [ kind: 'src', output: 'bin/main', path: '.apt_generated'])
                node.appendNode('classpathentry', [ kind: 'src', output: 'bin/test', path: 'test_groovy'])
                    .appendNode('attributes')
                    .appendNode('attribute', [name: 'test', value: 'true'])
            }
        }
    }
    jdt {
        javaRuntimeName = 'JavaSE-17'
    }
}

// in other envs: gradle flywayInfo -Pflyway.url=aaa -Pflyway.user=bbb -Pflyway.password=ccc
flyway {
    locations = ['filesystem:src/db/migration']
}
// ローカルはIDEでワンクリック操作できるように専用タスクを用意
tasks.register('flywayInfo_maindb', FlywayInfoTask) {
    url = 'jdbc:mysql://localhost:3306/maindb?useSSL=false&rewriteBatchedStatements=true'
    user = 'webapp'
    password = 'webapp'
    locations = ['filesystem:src/db/migration']
}
tasks.register('flywayMigrate_maindb', FlywayMigrateTask) {
    url = 'jdbc:mysql://localhost:3306/maindb?useSSL=false&rewriteBatchedStatements=true'
    user = 'webapp'
    password = 'webapp'
    locations = ['filesystem:src/db/migration']
}
tasks.register('flywayInfo_testdb', FlywayInfoTask) {
    url = 'jdbc:mysql://localhost:3306/testdb?useSSL=false&rewriteBatchedStatements=true'
    user = 'junit'
    password = 'junit'
    locations = ['filesystem:src/db/migration']
}
tasks.register('flywayMigrate_testdb', FlywayMigrateTask) {
    url = 'jdbc:mysql://localhost:3306/testdb?useSSL=false&rewriteBatchedStatements=true'
    user = 'junit'
    password = 'junit'
    locations = ['filesystem:src/db/migration']
}

domaCodeGen {
    dev {
        url = 'jdbc:mysql://localhost:3306/maindb?characterEncoding=utf8&useSSL=false&rewriteBatchedStatements=true'
        user = 'webapp'
        password = 'webapp'
        ignoredTableNamePattern = 'flyway_schema_history'
        sourceDir = file('generated/src/main/java')
        entity {
            packageName = 'com.example.sysid.model.entity'
            useListener = false
            entityPropertyClassNamesFile = file("$projectDir/.local/generator/doma-codegen-enum-mapping.properties")
        }
    }
}

bootJar {
    enabled = false
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = rootProject.name
            version = version
            from components.java
        }
    }
}
